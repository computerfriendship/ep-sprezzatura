-> intro

== intro ==
    
    YN: My whole life, I've struggled with the feeling that if I love something, the best thing for me to do is keep my distance from it.
    
    Maybe I think I'll mess everything up if I get too close.
    
    But when I'm really passionate about something, I'll admit: it feels good. At some point I stop even trying to resist.
    
    I don't get obsessed too often anymore. And when I do, I never know what it's going to be. But I get sucked in and then a few months of my life disappear.
    
    Sounds like typical Autism + ADHD to most people, but I never got diagnosed with either. It's probably too late for that now.
    
    Whatever. I indulge in a little hyperfixation from time to time.
    
    I don't think about it that much.
    
    The only reason I'm ruminating about it now is, well...
    
    This time one of my special interests paid off. Big time.
    
    // Sprezzatura drop?
    
    Parked on the curb, in front of my parents' house, is the van where I will eat, sleep, rehearse, and everything in between for the next three weeks.
    
    I'm not sure if the van life is a good fit for me, but it's just three weeks. Plus, I'll have good company!
    
    I'm going on tour. With Ear Piercings.
    
    // Ear. Motherfucking. Piercings.
    Ear. Piercings. Rascal Domino's traveling sideshow of wonder and whimsy.
    
    It's going to feel like a vacation.
    
    Sure, a little out of my comfort zone, maybe a lot actually.
    
    But an opportunity like this comes by once in a lifetime.
    
        -> meet_the_band
== meet_the_band ==

    YN: Paz (they/them, squirrel) is the first to hop out of the van, and they practically run at me.
    
    They stop short of running me over...
    
    And give me a huge smile.
    
        -> paz
    
    = paz
    
        Paz: I can't believe I'm really seeing youu! In real life!
        
        You kinda just look, uh, exactly how I expected you to! Huh!
        
        Wooooah, that wuz awkward! Sorry, I'm like, no filter sometimes.
        
        Where's ur stuff? I'll start moving it out.
        
        * [It's all at the bottom of the stairs]
        
            Me: I got everything at the bottom of the stairs!
            
            Paz: Swagalicious!
        
            -> paz2
            
        * [I don't know]
        
            Paz: No probz! I'll find it!
        
            -> paz2
    
    = paz2
    
    YN: They start past me towards the house, looking back to say "I meant it in a good way tho!"
    
    Paz is high energy. I do not usually know what to say back to them, if I ever have time to get a word in.
    
        -> endo
    
    = endo
        
        YN: I'm so lost in star-struck thought that I don't notice a taller, sallower looking man sidle up to me.
        
        Oh my god. That's DJ Endocrinologist (he/him).
        
        He's a DJ and an Endocrinologist and his best friend is a 2008 MacBook Pro.
        
        Rumor has it that he owns the only surviving copy of iWork '09: Alan L. Hart Edition. And it taught him how to synthesize testosterone in a lab and perform a perfect hysterectomy.
        
        Oh. I'm staring through him.
        
        I refocus my eyes to find that he is staring at *me*.
        
        DJ: "Sorry. Paz is right, you know, we really never got to look at you before."
            
        * [Play it cool]
            
            Me: "It's cool." I say.
            
            DJ: It's cool.
            
            YN: He's still studying me with his eyes.
            
                -> endo2
        
        * [Fangirl (gender neutral) about it]
            
            YN: I struggle to express any one emotion in particular, and end up squealing and wringing my hands.
            
            DJ: Oh, I don't mean to stare!
            
            How awkward of me.
            
            YN: We both look at the ground.
            
            He looks back up.
        
                -> endo2
    
    = endo2
        
        DJ: There really are no pictures of you out there, you know.
        
        It's atypical for someone in music.
        
        Not that it's bad...
        
        Me: Yeah, I just keep a low profile I guess. Really glad you guys found me, and didn't expect me to be famous or anything.
        
        DJ: Well, you know, we were auditioning for a certain sensibility. Looking for someone who really gets *us*. All the better if they're a little mysterious, I think.
        
        Me: I really only have social media to follow you guys.
        
        But it's not to be mysterious, it just, you know, internet stuff doesn't feel real.
        
        DJ: Feels like, just a model of yourself that's all cleaned up?
        
        Me: Yeah!
        
        DJ: I get it.
        
        YN: We stand in silence for a while. The sun beats down on us but it's nice.
        
        Ear Piercings is my favorite band. Maybe not all time, I don't usually rank stuff. But they've been my special interest of the last couple months.
    
        That's exceptionally long for me to care about one thing.
        
        Finally I have something to say.
        
        Me: You know, getting into Ear Piercings made me so much better at guitar.
        
        I know you might not need that from me, 'cause I'm mainly here to sing and all, but it's true!
        
        I've spent all the free time I had the last few months just singing all your songs and I usually accompany myself on guitar.
        
        DJ: I bet all that practice has paid off!
        
        YN: His smile is very cautious. Like he really wants to be excited for other people, but he's seen everything.
        
        He looks up at the sky and wanders off.
        
            -> eerie
        
    = eerie
        
        YN: I don't want to stand awkwardly on the front lawn so I walk towards the van.
        
        In the drivers seat is the legendary Eerie Pierce (any pronouns, cat/dog).
        
        Hir hair is floofy. Hir nose is always wet. Like a dog.
        
        He's flossing hir perfect teeth.
        
        He looks over but can't make a normal face because he is flossing.
        
        Still, I can tell it's a smile in spirit.
        
        He tries to wave too but hir other hand is busy.
        
        I look closer.
        
        He is reinforcing the stitches in an old patch on hir jacket with the used floss.
        
        Eerie: What's up?
        
        Me: Nothing much.
        
        // if you know where your bags are: "i've been preparing so it's not a big rush today"
        // if you didn't: "uh, just been taking things as they go"
        
        Eerie: Ayyyy, new kid! Didn't think we'd end up in white-picket-fence-world to pick you up.
        
        Who we rescuing you from?
        
        * [Don't get personal]
        
            Me: Just a bad housing market is all. Still with my folks.
            
            Couldn't get anything better at the prices I can pay.
            
            Eerie: Maaan, you picked the wrong business if you wanna move out of here!
            
            Can I call you man?
            
            But yeah, damn right it doesn't get better than this.
            
            I just couldn't though. I just can't get along in neighborhoods like this.
            
            People are always looking at me funny.
            
            The downtown places I end up are never this swanky, but hey!
            
            At least in the city you can't do anything to stand out.
            
            YN: Looks like he's noticed that I haven't said anything.
            
                -> eerie2
                
        * [I'm getting away from my parents]
            
            Me: My parents.
            
            It gets pretty stifling living with them.
            
            Eerie: Yeah, it's gotta be.
            
            I know the road life isn't easy and all, but at least you don't get your folks buggin' out at you for being at too many parties.
            
            They'll say, "oh, your hobby looks fun!" and they don't think you're really working.
            
            But it's hard out there!
            
            And they say we don't wanna work. Like uh, we can, just make it something actually worth doing.
            
            I think, anyway.
            
            YN: Looks like he's noticed that I haven't said anything.
            
                -> eerie2
            
        * [It's not really a rescue]
        
            Me: It's not like that, it's fine here.
            
            Eerie: Oh. Yeah, just joking around!
            
            YN: He smiles more than a little sheepishly.
            
                -> eerie2
        
    = eerie2
        
        YN: He looks a little defeated.
        
        Eerie: Sorry, got ahead of myself.
        
        I'm Eerie, it's good to see you. We're gonna have fun.
        
        YN: He reaches out hir hand for a surprisingly formal handshake.
        
        My fingers feel fishy in hir grip, but I don't mind how firm it is.
        
            -> paz3
                
    = paz3
        
        YN: Paz is back with my things.
        
        They throw the bags through the double doors at the back of the van, and crawl through, slamming the doors behind them.
        
        Eerie looks back down to the curb, one hand on the wheel, the other reaching out towards me.
        
        Eerie: If we're gonna make it to Spencerport in time we've gotta book it. Everyone in!
        
        YN: The door is open. I climb up. I grip the railing to destiny. I'm in the passenger seat of Ear Piercings' van.
        
        Ear. Piercings.
        
        Destiny.
    
        ->rascal
            
    = rascal
    
        YN: We're rolling out of my neighborhood.
        
        The scenery changes but only slowly.
        
        At some point the suburb kinda just turns into highway, there's not much of an indication until it happens and we're on the interstate.
        
        I haven't forgotten that Rascal Domino is also in this van with us.
        
        He's curled up, purring, at the center of a circle of lit candles that drip wax onto his blankets.
        
        I'm not gonna try talking to him yet.
        
        I might just be a little star-struck, even more than I am with the rest.
        
        I also do not want to wake him, because he is a cat.
        
        Cats gotta sleep, after all.
        
        Some of the other Piercings are animals too.
        
        But in society, they function closer to humans than wild animals.
        
        Rascal is a cat through and through.
        
        I get the sense that he sleeps in the daytime both out of respect for rock-and-roll's debaucherous roots, and because he needs his blanky time under the covers.
        
        And I am not going to take that from him.
    
        Eerie says that now might be our last chance to get some rest for a while, so I curl up as best I can in the passenger seat for a few hours.
    
            -> van_practice
== van_practice ==
        
        YN: I'm shaken by someone and I gradually come to my senses.
        
        It's Paz, grinning down at me.
        
        Paz: We gotta practice!
        
        Wake up!
        
        YN: They shake me again.
        
        Me: "Practice? In here? How?" I ask.
        
        YN: There's not much room in the van. Barely space for Paz's drum kit, which they're setting up in the far corner.
        
        Me: What are we gonna do for a PA?
        
        Eerie: Cars are full of speakers!
        
        YN: I look to see Eerie wiring DJ Endo's laptop directly into the van's stereo system with just some alligator clips.
        
        The speakers crackle and hum, and suddenly a wild collage of samples emerges. Gentle sounds, like the quickest snippets of dreams I can't remember. They wash peacefully over the breakfast bar wrappers and crushed energy drink cans.
        
        Eerie reaches back behind the drivers' seat and picks up a bass.
        
        Ze shouts to me over the noise.
        
        Eerie: Bass is in left channel, laptop is in right! I'm gonna patch you through the laptop!
        
        I can feel the frame of the van rattle as he soundchecks bass. Paz hands me a microphone and rushes back to the kit.
        
        * [Sound-check your voice] -> tap_the_mic
        * [Look for Rascal] -> look_for_rascal
        
        = look_for_rascal
        
            YN: I wonder where Rascal is.
            
            Me: Where's Rascal? Is he gonna be here?
            
            YN: Just then, a paw comes out from behind me and taps the microphone. I look over my shoulder.
            
            It's Rascal Domino! Hanging from the car seat headrest.
            
            RD: Shall we?
            
            YN: His drawl is more Nashville, Tennessee in person.
            
                -> song
            
        = tap_the_mic
        
            YN: First I tap the mic.
            
            I hear a pop. Through Endo's setup, even the pop feels musical.
            
            Then I try to sing.
            
            I haven't warmed up and I just woke up from a nap, and normally in a situation like that, I'd feel the congestion in my throat making my voice raspy.
            
            But I come through soft and clear through the speakers.
            
            It's more than a little shocking.
            
                -> song
        
        = song
        
            YN: Rascal kicks up the band with an unexpected but admittedly genius guitar chord.
            
            Now things are getting interesting.
            
            I know, for a fact, that my voice does not usually behave this way.
            
            I've trained for so long to understand how my voice works, to identify all its weak points and learn how to conceal them.
            
            Now that I'm with the band, I can just melt right in to their sound.
            
            I was *made* for it.
            
            It's as if years of voice lessons are suddenly irrelevant.
            
            It's a little bit frustrating.
            
            I was so excited to show them how hard I've worked to get this good, and now I'm just a natural? Not fair.
            
            Rascal looks at me. *Through* me.
        
            Rascal: You're thinking. Don't think.
        
            Maybe I should have fun with this...
        
            -> rascal_shows_you_music
        
        -> END
== rascal_shows_you_music ==
    
    YN: Rehearsal was fun after that. We know what we're gonna do tonight for the show, and I felt confidence in my abilities like never before.
    
    I stare down the road ahead of me in the passenger's seat.
    
    The seat and the dashboard are covered in little crumbs, dust, and little hairs.
    
    I pick at the surfaces occasionally but it makes no difference.
    
    Rascal slinks between Eerie's seat and mine, and springs up onto the cupholders.
    
    He's in his usual ragged navy blue jacket.
    
    "Mrrrrrow".
    
    It's a low and gravely purr.
    
    Rascal: Evening, madam.
    
    Can I call you madam?
    
    * [uh, sure] -> madam_yes
    * [no thanks] -> madam_no
        
    = madam_yes
        YN: He stretches out his front paws as if about to introduce a circus act.
        
        Rascal: Ahh, lovely! Tranquil evening we're having, madam?
        
        Me: uh, yeah!
        
        -> rascal_ask
        
    = madam_no
        Rascal: Very well. That's besides the point anyhow.
    
        -> rascal_ask
    
    = rascal_ask
    
    { madam_yes:
        Rascal: I wanted to know, madam, if you're up for some music listening!
    }
    
    { madam_no:
        Rascal: I only wanted to know, madam, if you're up for some music listening!
    }
    
    And also some cuddling. I need snuggles.
    
    YN: That jacket looks like it could've come straight from the Civil War. I have no way of confirming or denying that.
    
    Eerie looks at me. "He's very soft, you know."
    
    // Make this part skippable entirely?
    
    * [cuddle]
        -> cuddles
    * [no cuddles]
        -> no_cuddles
        
    = cuddles
        Rascal: Ahh, magnificent!
        
        YN: Rascal hops from the cupholders to the dashboard, and down onto my lap.
        
        A few empty styrofoam cups are displaced in the leap and they clatter onto the floor.
        
        Eerie's right, his fur is very soft.
        
        But the jacket makes it not entirely pleasant when he shifts about on my lap.
        
        It's bumpy, even a bit spiky.
        
        It's not a studded crust punk jacket like Eerie's, so what's up with that?
        
        Finally, Rascal rolls over onto his back. Five or six cassette tapes spill from his pockets!
        
        Oh, that's what was digging into my thigh.
        
        // ONLY say madam if you are a madam
        { madam_yes:
            Rascal: Ahh yes, those! Apologies for the indignity, madam. I bet those are pretty sharp.
        }
        
        { madam_no:
            Rascal: Ahh yes, those! My sincerest apologies for the indignity.
        }
        
        YN:  He looks up with his wizened old-cat eyes and I forgive him.
        
        Rascal: My selections for the night.
        
        YN: He spreads the cassettes out over his fuzzy little cat tummy.
        
        Rascal: What shall it be?
        
        // CGs for each cassette?
        
        -> intensa
        
        = intensa
        
        YN: Option one: *Intensa* by The Transworld Depravity.
        
        The album that started it all. The Intensa genre is, in fact, named after this release.
        
        The Transworld Depravity recorded this album as their debut in 1977, but it's not really that simple.
        
        The band comes from Varnishing Point: a secretive artists' colony that no one may enter, and no one may leave.
        
        Residents of the colony, called "pointers," are tasked with creating art from future time periods. They do so under extreme conditions, often under threat of torture.
        
        The colony believes it must create ten thousand years of art history in only a hundred years. That's gotta be rough.
        
        // We need a varnishing point relative time calculator. What does 1977 map to during the flattening?
        *Intensa* was meant to be an album from the year 4775. People from this time were never meant to hear it.
        
        We're never meant to hear anything made in Varnishing Point, for that matter.
        
        How does Rascal have it?
        
        It's certainly a tempting choice.
        
        -> hounds_of_kismet
        
        = hounds_of_kismet
        
        YN: Option two: *Over Him*
        
        // Ideas: Girl Trio, Men Punch Walls, two spies communicating on a number station who decided to make beats instead and it was captured by a random shortwave radio operator?
        
        -> END

    = no_cuddles
        Rascal: Quiet time instead? Very well, very well. I'm going in for a nap.
        
        -> END
        
    -> END
    
== paz_convo ==

    Rascal returns to the back of the van for another nap.
    
    Paz is concerned because you were so nervous about listening to music.
    
    They talk to you about their reasons for being on the road, and how they're nervous too.
    
    But they assure you it will be okay.
    
    You have each other, after all!
    
    Then they want you to know if you would listen to the new song they wrote.

    * [listen to Paz's song]
        -> talk_like_paz
    * [don't listen to Paz's song]    
        -> paz_done_with_you
    
    = talk_like_paz
    
        Yeah, I'd better listen to something Ear Piercings. It'll remind me why I'm here.
        
        I still like Ear Piercings, I still like their songs!
        
        I'm sure this one will be great. Maybe even a fresh angle on their sound.
        
        // boilerplate below
    
        You compliment the song, but you are more enthusiastic than usual.
        
        In fact, your are even smiling with your teeth!
        
        Like Paz does...
        
        And when they ask about it, you use all of Paz's vocal ticks in your answer.
        
        One by one.
        
        Everyone calls you out for being super star struck, and Paz says they need a minute in the back.
        
        -> endo_eerie_choice
        
    = paz_done_with_you
    
        I can't.
        
        What if I don't like it?
        
        I never would have thought that possible before, but now?
        
        I can't bring myself to hurt Paz like that.
        
        But I can't help that I don't care anymore!
        
        Fuck, I'm gonna have to quit!
        
        Paz has been looking at me, concerned.
        
        I just realized I haven't said anything.
        
        I blurt out "I c- sorry, I've gotta go I won't be able to!"
        
        I've gone into a heel turn with the thought that I could go somewhere else, but there isn't somewhere else.
        
        We're in a van with very little privacy.
        
        Paz just looks at me, head cocked.
        
        Paz: "You sure?"
        
        Me: "It's not, I-I don't"
        
        Paz: "Okay I'm gonna need a minute, uh, bye? For now? Okay yeah."
        
        Paz walks up to the back doors and lifts up a blanket I hadn't noticed.
        
        They pull it over themself and curl up into a ball.
        
        I am left standing awkwardly in the aisle.
        
        Eerie's in the driver's seat right behind me. Endo was leaning on a side wall this entire time; I just hadn't noticed.
        
        There really is no place to hide in this thing.
    
        -> endo_eerie_choice
    -> END
    
== endo_eerie_choice ==
    
    DJ Endocrinologist and Eerie approach you together.
    
    They express concern over your behaviour.    
    
    * [Talk to Endo]
        -> endo_convo
    * [Talk to Eerie]
        -> eerie_convo
        
    -> END
    
== endo_convo ==

    DJ Endo takes you aside.
    
    DJ: Okay. First of all, that was not cool.
    
    Stealing their typing quirk and everything.
    
    I don't know if it's a crush or something?
    
    YN: Now I'm really red in the face.
    
    DJ: But, my gut says it's weirder than that.
    
    He tells you about how, when he was young, he wanted to be like the male characters in his favorite art.
    
    Not only that- he felt he was them, deep inside.
    
    Like when he pretended to be one of them, he was more real than he was when he 'pretended' to be himself.
    
    Being a character clearly defines your traits. Characters are so powerful because they are finite. A character is only made up of the descriptions that are created about them by an author.
    
    A person is more messy. What person can be described fully in a piece of art?
    
    Nobody- but definitely not one with a big imagination, like you.
    
    It's hard to live in the world as a real person. It's easier to compress yourself, you know? Make yourself a character.
    
    And to some extent, that's fine!
    
    But not when the character you're copying is a real person.
    
    And not when it causes you to forget who you really are.
    
    The core you- you might express it through performing any number of characters in your life.
    
    And maybe you can't take in your full complexity all in at once.
    
    But it's there.
    
    And through relating to outside things, he was able to find out what he was.
    
    Me: I mean, what you're saying sounds right. But I never considered any of this before.
    
    I never realized how much of what you're saying has happened to me, too.
    
    I don't even know...
    
    I've been getting into things for so many years, how can I remember what I was like? Before anything changed me?
    
    DJ: You've gotta get back to that. It's the only way you're gonna be happy in the long run.
    
    I know that sounds harsh but trying to run from yourself... it just doesn't work.
    
    YN: We sit in silence for a while. He looks down at his hands and I stare at the wall.
    
    Me: You know, are you absolutely sure there is such a thing as a core you?

    YN: At this, he shifts over to me. He gently takes my hand into his.
    
    He's very careful with his bedside manner.
    
    But his smile- it's excited.
    
    Like he's caught me, and he's looking forward to the part he knows is coming next.

    Uh oh.
    
    Is he a real doctor?
    
    Before I even have time to ask, he's cut the silence with the zipper of his medical bag.

    DJ: You know, the only way I could really ever figure that out was to take that leap of faith.
    
    The one we all have to take.
    
    Well, all the trans people anyway.
    
    Make the body match the mind!
    
    Except normally, what you have to do first is realize what you are- then go about all that hard work.
    
    I hated how that phase made me feel.
    
    All the excitement, knowing it was possible to change my life! But all the second guessing.
    
    Am I really gonna like this? Am I really even trans? Maybe I got carried away- maybe I'm just trying to be like someone else!
    
    YN: He's pulling a very long syringe out of the bag. It's pre-filled.
    
    DJ: This- this, is the new HRT. I imagine it will replace the contemporary methods.
    
    Say goodbye to "would you press the button if it would make you a boy, no strings attached?"
    
    You don't have to know. Just give it a try! If you weren't really trans, it won't do anything!
    
    If you were trans all along, it gets to work.
    
    It *knows* what you want.
    
    He takes my wrist.
    
    Don't worry. There *is* a real you. You'll find it.

    * [Take the potion] -> potion_ending
    * [Refuse the potion] -> eerie_convo
    
    = potion_ending
        -> END
        
== eerie_convo ==

    Eerie waves me up to the passenger seat.
    
    He's at the wheel just driving through the night, staring down the highway.
    
    Eerie: So, uhhh, what was that about?
    
    Me: Back there?
    
    Eerie: You've been having strange encounters all day. Like, not the usual things we get when we meet people.
    Yeah, they can be a little weird about us, assume things that aren't true, act like they know us, etc. That's showbiz, it's fine.
    
    
    But you're our friend! And you don't seem, uh, comfortable?
    
    Me: Not yet, I guess. It's been... weird.
    
    Eerie: Do you wanna talk about it?
    
    * [Tell Eerie about DJ Endo] -> new_jersey_ending
    * [Tell Eerie about Paz] -> paz_ending
    * [Tell Eerie about Rascal] -> rascal_ending
    
    = new_jersey_ending
    
        YN: Ze motions for me to lean in. He's gonna whisper something in my ear.
        
        Eerie: Not gonna lie, I was listening.
        
        I don't want Endo giving you any bad advice. He's got his own demons.
        
        That means he can give great advice sometimes!
        
        But he's also pretty good at taking the point waaay too far.
        
        He thinks you've gotta fight fire with fire every time, if that makes any sense.
        
        But yeah, uh...
        
        YN: Eerie wrings hir hands. Ze looks afraid to finish the question.
        
        Eerie: Do you just feel like you're always turning into someone else?
        
        YN: Hir smile is warm. Caring even.
        
        Me: Oh man...
        
        Can I call you man?
        
        Eerie: Yeah.
        
        Me: I can't say much else about it. Up until today I've never realized how much things affect me.
        
        And now, like, I don't want people to notice.
        
        I don't want them to, I don't know, start thinking everything I do comes from something I saw? Or heard? And none of it's *me*?
        
        Eerie: Maybe that- that way of feeling things, *is* the real you?
        
        Hey. What if I give you a gift. Hold out your hand?
        
        * [Trust Eerie] -> nj_kandi
        * [Don't trust Eerie] ->no_nj_kandi
        
        = nj_kandi
        
            YN: My arm quivers, but I reach out to Eerie.
            
            He seems safe, and it's not like the night can get any worse.
            
            He clasps my hand and slides a kandi bracelet across to my wrist.
            
            He lets go, and the elastic band fits on with a snap.
            
            The beads are orange, red and blue, and the letter beads read "2201" on one side, and "-16" on the other.
            
            Me: 2201? Negative 16?
            
            Eerie: It's the highest and lowest elevations in the state of New Jersey, in feet!
            
            Me: Wha- New Jersey? Why?
            
            Eerie: All my kandi is New Jersey inspired! Especially by geography stuff.
            
            YN: Eerie can no longer hide his dopey grin.
            
            Eerie: I'm not expecting you to get super into geography with me just 'cuz I gave you some New Jersey kandi. But hey- if it makes you feel any better, I'm weird about something too.
            
            YN: Now I can't help but laugh. He's funny, but I'm also so relieved.
            
            Me: I'm not gonna suddenly develop a thick Joisey accent just from a bracelet!
            
            Eeire: Hey, we don't tawk like that!
            
            YN: Eerie laughs too, and even slaps hir knee.
            
            Eerie: Besides, that's my point!
            
            Maybe when you change it's 'cuz you found something you didn't know you wanted!
            
            It's not *everything* that changes you, just what you think is cool, yeah?
            
            Me: Huh. Yeah, maybe!
            
            Eerie: Sorry New Jersey geography isn't cooool enough for ya.
            
            Me: Hey!
            
            Eerie: I'm just teasing ya.
            
            Hey, let me know if you notice anything different in the next couple days, yeah? Geography can realy sneak up on you.
            
            We'll have plenty to talk about!
            
            -> sleep_extra
            
        = no_nj_kandi
            -> END
        
            -> END
            
    = paz_ending
    
        Eerie: Paz is upset, yeah?
        
        YN: I look down at my feet.
        
        Eerie: It looks like you understand why it was weird for you to do that.
        
        Stealing their typing quirk! Damn right they're upset.
        
        But hey, imitation is the sincerest form of flattery, yeah?
        
        Maybe a little too sincere for a cool critter like Paz, but I bet if you talked to them about it they'd understand.
        
        There's gonna be other opportunities to get excited about stuff together- you'll do it better next time.
        
            -> sleep_extra
        
    = rascal_ending
        -> END
    
    = sleep_extra
    
        Eerie smiles and returns to the wheel.
        
        Get some sleep, yeah?
        
        You can sleep up here. I'll be up all night, I won't let anyone talk shit behind your back. You're fine.
        
        Believe me. We've had worse bandmates.
        
        Had to drive a stake through this one girl's heart one time.
        
        And she was the only one who could play bass!
        
        After that I took up bass. Not letting that happen again. Right before a gig and everything!
        
        YN: He groans in mock disbelief.
        
        Eerie: Anyway, sleep tight!
            
            -> END
    
    -> END